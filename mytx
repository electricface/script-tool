#!/usr/bin/perl
use strict;
use warnings;
use 5.14.2;

use Config::Tiny;
use Data::Dumper;
use Getopt::Long;

my ($opt_update_tx_config,$opt_commit, $opt_push_lang, $opt_dump, $opt_stash_clear, $opt_help, $opt_list_project,$opt_project, $opt_resource , $opt_push_pot, $opt_stat, $opt_list_source_file, $opt_list_po_file, $opt_info, $opt_list_resource, $opt_auto );

if (!exists $ENV{GIT_REPO_DIR} || $ENV{GIT_REPO_DIR} eq '' ){
	say STDERR 'Not set the $GIT_REPO_DIR environment variables.';
	exit 2;
}
my $GIT_REPO_DIR= $ENV{GIT_REPO_DIR};
my $TRANS_DIR="$GIT_REPO_DIR/trans";
my $GITHUB_ACCOUNT='electricface';
my $PROJECT_RESOURCE_CONFIG_FILE = "$TRANS_DIR/.tx/project-resource.ini";
my $TX_CONFIG_FILE = "$TRANS_DIR/.tx/config";
my $cfg = Config::Tiny->new->read( $PROJECT_RESOURCE_CONFIG_FILE );
my $tx_config = Config::Tiny->new->read( $TX_CONFIG_FILE );
my $MINIMUM_PERC=30; # pull from tx when > 30%
my @COMMIT_FILE_EXTENSIONS = qw(po pot ini desktop);
Project->init($cfg);

GetOptions(
	"help" => \&print_help,
	"list-project" => \&Project::print_project_list,
	"stat" => \$opt_stat,
	"list-source-file" => \$opt_list_source_file,
	"list-po-file" => \$opt_list_po_file,
	"info" => \$opt_info,
	"dump" => \$opt_dump,
	"resource=s" => \$opt_resource,
	"list-resource" => \$opt_list_resource,
	"project=s" => \$opt_project,
	"push-pot" => \$opt_push_pot,
	"push-lang=s" => \$opt_push_lang,
	"auto" => \$opt_auto,
	"install" => \&install,
	"stash-clear" => \$opt_stash_clear,
	"commit" => \$opt_commit,
	"update-tx-config" => \&Project::update_tx_config,
);


# --project STRING
if ( $opt_project ) {
	my $proj = Project->new( $opt_project );
	# --list-resource
	if ( $opt_list_resource ){
		say for $proj->resources;
	}

	# --auto
	elsif ( $opt_auto ){
		$proj->git_pull_from_linuxdeepin;
		$proj->update_po_from_transifex;
		$proj->update_po_from_local;
		for my $r ( @{ $proj->{resource_objs} } ){
			$r->update_pot_file;	
			$r->merge_po_with_pot;
			$r->update_locale_config_ini;
			$r->generate_mo;
			$r->push_pot;
		}

	}

# 	--commit
	elsif ( $opt_commit ){
		$proj->commit;
	}

# 	--push-pot
	elsif ( $opt_push_pot ){
		for my $r ( @{ $proj->{resource_objs} } ){
			$r->push_pot;
		}
	}

	# --dump
	elsif ( $opt_dump ){
		print Dumper $proj;
	}
		
	
#	--stash-clear
	elsif ( $opt_stash_clear ) {
		$proj->empty_all_changes;
	}
	else {
		say "wrong used --project";
	}
}

# --resource STRING 
if ( $opt_resource ) {
	my $resource = Resource->new( $cfg , $opt_resource );

	# --stat
	if ( $opt_stat ) {
		my $ref_po_stat = $resource->po_statistics;
		print_stat( $ref_po_stat );
	} 

#	--push-pot
	elsif ( $opt_push_pot ) {
		$resource->push_pot;	
	}

#	--push-lang zh-TW
	elsif ( $opt_push_lang ){
		$resource->push_lang( $opt_push_lang );
	}


	# --list-source-file
	elsif ( $opt_list_source_file ){
		my $ref_sf_list = $resource->source_file_list;
		for (sort keys $ref_sf_list) {
			say "xgettext option : $_";
			for my $f ( @{ $ref_sf_list->{$_} } ){
				say "  " . $f; 
			}
		}
	}

	# --list-po-file
	elsif ( $opt_list_po_file ){
		my $ref_po_file_list = $resource->{lang_po_list};
		for ( sort keys $ref_po_file_list ){
			say "$_  \t" . $ref_po_file_list->{$_} ;
		}
	} 

	# --info
	elsif ( $opt_info ){
		my $key_color="\e[38;5;208m";
		for (qw( project_root pot_file ) ){
			say "$key_color$_:\e[0;m $resource->{$_}";
		}
		my $po_file_filter = $resource->{data}{po_file_filter};
		say $key_color. "po_file_filter: \e[0;m$po_file_filter";
		for ( sort keys $resource->{data} ){
			if ( /^source_file_regex/ ) {
				say "$key_color$_: \e[0;m" . $resource->{data}{$_};
			}
		}
		my $resource_name = $resource->{resource_name};
		my $project_name = $resource->{project_name};
		
		my $url = $resource->{data}{resource_url};
		$url =~ s/$project_name/\e[38;5;2m$project_name\e[0;m/;
		$url =~ s/$resource_name/\e[38;5;3m$resource_name\e[0;m/;	
		say $key_color."resource_url: \e[0;m". $url;

	}

#	--dump 
	elsif ( $opt_dump ){
		print Dumper $resource;
	}

	else {
		say "wrong used --resource";
	}
}


sub print_help {
	print 
"--help
--install
--list-project
--project PROJECT_NAME
	--list-resource
	--auto
	--commit
	--dump
--resource RESOURCE_NAME
	--stat
	--info
	--dump
	--list-po-file
	--list-source-file
	--push-lang LANG
";
	exit;
}


package Project {
use List::MoreUtils qw(any);
	my %project_list;
	my $config;

#  Project->init($cfg);
	sub init {
		my $class;
		( $class, $config ) = @_;
		my @sections = keys %{ $config };
		for ( @sections ){
			next if $_ =~ /^_/;
			my ( $dir_name , $git_branch ) = split /~/;
			$git_branch = "master" if !defined $git_branch;	
			my $sub_module_name;
			( $dir_name , $sub_module_name ) = split /\./, $dir_name;
			my $project_name;
			if ( $git_branch eq 'master' ){
				$project_name = $dir_name;
			} else {
				$project_name = "$dir_name~$git_branch";
			}
			if ( !defined $sub_module_name ){
				$project_list{ $project_name } = ['_'];
			} else {
				push @{ $project_list{ $project_name } }, $sub_module_name;
			}
		}
	}
	
# my $project = Project->new('dde~2013');
	sub new {	
		my ($class, $project_name ) = @_;
		my $git_branch = 'master';
		my $project_dir_name = $project_name;
		if ( $project_name =~ /(.*)~(.*)/ ){
			$project_dir_name = $1;
			$git_branch = $2;	
		}

		my @resources;
		if ( !exists $project_list{$project_name} ){
			say STDERR "project `$project_name` undefined.";
			exit;
		} else {
			my $ref_project = $project_list{$project_name};
			my $count = scalar values $ref_project;
			my $type;
			if ( $count  == 1 && $ref_project->[0] eq '_' ) {
				#Single;
				$type = 's';	
				push @resources , Resource->new( $config, $project_name );
			} else {
				#Multiple
				$type = 'm';	
				for (values $ref_project ){
					push @resources , Resource->new( $config, $project_name.'.'.$_ );
				}
			};
			return bless { 
				name => $project_name ,
				git_branch => $git_branch,	
				dir_name => $project_dir_name,
				data => $ref_project ,
				resource_objs => \@resources,
				count => $count ,
				type => $type ,
			} , $class;
		}
	}

# project->commit
	sub commit {
		my $self = shift;
		my $dir_name = $self->{dir_name};
		my $proj_path = $GIT_REPO_DIR .'/'. $dir_name;
		chdir $proj_path or die "can chdir $!";

		for my $ext (@COMMIT_FILE_EXTENSIONS) {
			system "git add *.$ext";
		}
		my @git_status = qx(git status -s);
		my @file_changes_info;
		my %change;
		for my $s (@git_status) {
			if ($s =~ /^(A|M|D).*\.(\w+)$/) {
				my $mod_flag = $1;
				my $file_ext = $2;
				if ( any { $_ eq $file_ext } @COMMIT_FILE_EXTENSIONS ){
					$change{"$mod_flag: $file_ext"}++ ;
					push @file_changes_info, $s;
				}
			}
		}
		my $commit_str = '';
		for ( keys %change ) {
			$commit_str .= "$_($change{$_}); ";
		}
		my $file_changes = "[ File Changes ]\n" . ( join '',sort @file_changes_info );
		print $file_changes . "\n";
		print "[ Changes Count ] ".$commit_str . "\n";
		my $prompt = "\nInput some commit message:";
		print $prompt;
		my $input = <STDIN> ;
		chomp $input;
		if ( $input =~ /^ok\s+(.+)/ ) {
			$commit_str = "$1  $commit_str";
			say "\$ git commit -m \'$commit_str\'";
			system "git commit -m '$commit_str' ";
			say "\$ git push";
			system "git push";
		}	
		else {
			say "Cancel\n\$ git reset HEAD *";
			qx"git reset HEAD *";
			exit;
		}

	}

	sub update_po_from_local {
		my $self = shift;
		my $local_trans_dir = "$TRANS_DIR/$self->{dir_name}";
		my $git_repo_dir = "$GIT_REPO_DIR/$self->{dir_name}";
		my $cmd = "rsync -rtpv --exclude='en.po' --exclude='*zh_CN*' --exclude='messages.mo' ".
			"--exclude='*.pot' --exclude='*.sw?' $local_trans_dir/ $git_repo_dir";
		my %po_regex_list;	
		for my $r ( @{ $self->{resource_objs} } ){
			my $po_file_regex = $r->{data}{po_file_filter};
			$po_file_regex =~ s/<lang>/([A-Za-z_]+)/;
			$po_file_regex =~ s(<project_root>/)();
			$po_regex_list{ $po_file_regex } = $r->{full_name};
		}
		say "\e[38;5;207mupdate po from `$TRANS_DIR`\e[0;m";
		open my $cmd_output, "$cmd|";

		my $cur_regex ='';
		my $cur_resource;
		my $lang = '';
		my $change_flag;
		my $match_flag;
		my $match_count=0;
		for my $line ( <$cmd_output> ){
			chomp $line;
			if ( $line =~ /^$cur_regex$/ ){
				$lang = $1;
				$change_flag = 0;
				$match_flag = 1;
			} else {
				$match_flag = 0;
				for my $rx ( keys %po_regex_list ){	
					if ( $line =~ /^$rx$/ ){
						$match_flag = 1;
						$match_count++;
						$cur_resource = $po_regex_list{$rx};
						$cur_regex = $rx; 
						delete $po_regex_list{$rx};
						$change_flag = 1;
						last;
					}

				}

			}
			my $nl='';
			$nl= "\n" if $match_count != 1;
			print "$nl$match_count. \e[38;5;3m[$cur_resource]\e[0;m\n" if  $change_flag;
			print $lang . " " if defined $lang && $lang ne '' && $match_flag == 1;
		}
		close $cmd_output;
	}


#project->update_po_from_transifex
	sub update_po_from_transifex {
		my $self = shift;
		chdir $TRANS_DIR or die $!;
		my $ids = join ",", $self->tx_resource_ids;
		my $cmd = "tx pull -r '$ids' -a --minimum-perc=$MINIMUM_PERC";
		print "\e[38;5;207mpull po from transifex\e[0;m";
		system $cmd .q{|perl -n -e '
			use 5.12.4;
			my $oldfh = select STDOUT;
			$|=1;
			select $oldfh;
			chomp;
			if ( /Pulling translations for resource\s*(\S+)\s*\(source:\s*(.*\.pot)\)/) {
				my ($resource, $pot_file ) = ($1,$2);
				say "\n\e[38;5;6mPull $resource\e[0;m";
				say STDERR "pot file `$pot_file` not exists." unless -f $pot_file;
			} elsif ( /->\s*(.+):\s*.+\.po/ ){
				print "$1 ";
			} elsif ( /Done\./ ){
				print "\n";
			}'
		}
	}
	
#project->empty_all_changes
	sub empty_all_changes {
		my $self = shift;
		my $dir_name = $self->{dir_name};
		my $proj_path = $GIT_REPO_DIR .'/'. $dir_name;
			
		chdir $proj_path or die $!;
		system "git stash";
		system "git stash clear";

		print "Empty all the changes in project \"$dir_name\".\n";

	}
	

#project->git_pull_from_linuxdeepin
	sub git_pull_from_linuxdeepin {
		my $self = shift;
		my $dir_name = $self->{dir_name};
		my $proj_path = $GIT_REPO_DIR .'/'. $dir_name;
		chdir $proj_path or die $!;
		my $git_cfg = Config::Tiny->new->read("$proj_path/.git/config");
		my $origin  = $git_cfg->{'remote "origin"'}->{url};
		if ( $origin !~ m{github.com:$GITHUB_ACCOUNT/} ) {
			say "Bad remote orgin url";
			exit;
		}
		my $deepin_remote = $config->{'_git'}{$dir_name};

		#like https://github.com/linuxdeepin/deepin-screenshot.git
		if ( $deepin_remote =~ m{^https://github.com/linuxdeepin/.*\.git} ){
			$deepin_remote =~ s{^https://github.com/}(git\@github.com:);
		} else {
			say "bad config in file `$PROJECT_RESOURCE_CONFIG_FILE` section [-git] $dir_name";
			exit;
		};
		my $git_branch = $self->{git_branch};
		print "Remove untracked files from the working tree.\n";
		system "git clean -fd ; git stash && git stash clear";
		say "\$ git pull $deepin_remote $git_branch";
		system "git pull $deepin_remote $git_branch";
		if ($?) {
			say "git pull on error";
			exit;
		}
	}

	sub tx_resource_ids {
		my $self= shift;
		my @tx_resource_ids;
		my $ref_resources = $self->{resource_objs};
		for my $r ( @$ref_resources ){ 
			my $resource_id = $r->{project_name} .'.'. $r->{resource_name};
			push @tx_resource_ids, $resource_id; 
		}
		return @tx_resource_ids;
	}

	sub resources {
		my $self = shift;
		if ($self->{type} eq 's'){
			my $resource_name = $self->{resource_objs}[0]{resource_name};
			return  ( "$self->{name}.$resource_name" );		
		} else {
			return map { "$self->{name}.$_" } values $self->{data};
		}
	}

	sub print_project_list {
		say for sort keys %project_list;
		exit;
	}
	#	--update-tx-config

	sub update_tx_config {
		for my $p (keys %project_list ){
			my $proj = Project->new($p);
			for my $r ( @{ $proj->{resource_objs} } ){
				$r->update_tx_config( $tx_config );
			}

		}
		$tx_config->write($TX_CONFIG_FILE);

	}


}

package Resource {
	use List::MoreUtils qw(any);
	use File::Basename qw(dirname);
	use Config::Tiny;
	use File::Copy;
	use Data::Dumper;
	sub new {
		my ( $class, $config, $section ) = @_;
		if ( !exists $config->{$section} ) {
			say STDERR "resource `$section` undefined.";
			exit 2;
		}
		my ( $dir_name , $git_branch ) = split /~/ , $section;
		$git_branch = "master" if !defined $git_branch;
		my $full_name = $dir_name;
		($dir_name ) = split /\./, $dir_name;
		
		my $url = $config->{$section}{resource_url};
		my ($project_name, $resource_name);
		if ( $url =~ m{^https://www.transifex.com/projects/p/([^/]+)/resource/([^/]+)/$} ){
			($project_name, $resource_name) = ($1,$2);
		}


		my $project_root = "$GIT_REPO_DIR/$dir_name",
		my $pot_file = $config->{$section}{pot_file};
		$pot_file =~ s/<project_root>/$project_root/;

		my $po_file_filter = $config->{$section}{po_file_filter};
		( my $po_file_regex = $po_file_filter ) =~ s/<project_root>/$project_root/;
		$po_file_regex =~ s/<lang>/([A-Za-z_]+)/;

		my $po_dir = ( split /<lang>/ , $po_file_filter )[0];
		$po_dir = dirname($po_dir);
		$po_dir =~ s/<project_root>/$project_root/;

		my @po_files = qx(find $po_dir -iname "*.po" );
		my %lang_po_list;

		for ( @po_files ) {
			chomp;
			if ( /^$po_file_regex$/ ){
				my ($lang,$file) = ($1,$_);
				$lang_po_list{$lang} = $file;
			}
		}

		return bless { 
			data => $config->{$section} ,
			dir_name => $dir_name,
			full_name => $full_name,
			pot_file => $pot_file,
			project_name => $project_name,
			project_root => $project_root,
			resource_name => $resource_name,
			po_file_regex => $po_file_regex,
			po_dir => $po_dir,
			lang_po_list => \%lang_po_list,
		} ,$class;
	}

#resource->push_lang
	sub push_lang {
		my ($self,$lang) = @_;
		my $po_file_path = $self->{lang_po_list}{$lang};
		if ( ! defined $po_file_path ) {
			print STDERR "lang $lang not support.\n";
			exit 2;
		}
		my $trans_po_file_path = $po_file_path =~ s/$GIT_REPO_DIR/$TRANS_DIR/r;
		unless ( -f $po_file_path ) {
			print STDERR "file `$po_file_path` not exits.\n";
			exit 2;
		}
		
		copy($po_file_path, $trans_po_file_path) 
			or die "copy failed $!"; 
		print "copy `$po_file_path` file to $trans_po_file_path \n";
		my $project_name = $self->{project_name};
		my $resource_name = $self->{resource_name};	
		chdir $TRANS_DIR or die $!;
		print "\$ tx push -t -r $project_name.$resource_name -l $lang \n";
		system "tx push -t -r $project_name.$resource_name -l $lang \n";
		print "push lang \"$opt_push_lang\" to Transifex \n";
	}
	
#resource->update_tx_config
	sub update_tx_config {
		my ($self, $tx_cfg) = @_;
		my $section = $self->{project_name} . '.' . $self->{resource_name};
		$tx_cfg->{$section}{source_lang} = 'en';
		$tx_cfg->{$section}{type} = 'PO';
		
		my $source_file = $self->{data}{pot_file};
		my $dir = $self->{dir_name};
		$source_file =~ s/<project_root>/$dir/;
		$tx_cfg->{$section}{source_file} = $source_file;

		my $file_filter = $self->{data}->{po_file_filter};
		$file_filter =~ s/<project_root>/$dir/;
		$tx_cfg->{$section}{file_filter} = $file_filter; 

		print "[ $section ] \n";
		print Dumper $tx_cfg->{$section};	
	}


#resource->merge_po_with_pot
	sub merge_po_with_pot {
		my $self = shift;
		my @po_files = values $self->{lang_po_list};
		my $pot = $self->{pot_file};
		for my $po (@po_files){
			#say "msgmerge -s -U  '$po' '$pot' ";			
			qx(msgmerge -s -U  '$po' '$pot' 2>/dev/null );			
		}
		
	}

#resource->update_pot_file
	sub update_pot_file {

 		my $self = shift;
		my $pot_file = $self->{pot_file};
		my $sf_list = $self->source_file_list;
		print "\nwrite pot file : `$pot_file`\n";

		#backup pot file
		qx(mv $pot_file $pot_file.save ; touch $pot_file);
		for my $option ( sort keys %$sf_list ){	
			open my $xgettext , "|xgettext $option --no-location --from-code=utf-8 -s -j -f - -o $pot_file 2>/dev/null"
				or die "error $!";
			for my $f ( @{ $$sf_list{$option} } ) {
					say $xgettext $f;
			}
			close $xgettext
		}
	}

#resource->push_pot 
	sub push_pot {
		my $self = shift;
		my $pot_file_path = $self->{pot_file};
		my $trans_pot_file_path = $pot_file_path =~ s/$GIT_REPO_DIR/$TRANS_DIR/r;

		copy($pot_file_path, $trans_pot_file_path) 
			or die "copy failed $!"; 
		#print "copy `$pot_file_path` file to $trans_pot_file_path \n";
		
		my $resource_name = $self->{project_name}.'.'.$self->{resource_name};
		chdir $TRANS_DIR or die $!;
		print "\$ tx push -s -r $resource_name \n";
		system "tx push -s -r $resource_name";
	}	

#resource->update_locale_config_ini
	sub update_locale_config_ini {
		my $self = shift;

		my $locale_config_file = $self->{data}{locale_config};

		if ( defined $locale_config_file ){
			$locale_config_file =~ s/<project_root>/$self->{project_root}/;
			if ( ! -f $locale_config_file ) {
				say "Not found 'tools/locale_config.ini' file";
				return;
			}
		} else {
			say "update_locale_config_ini : resource not defined locale_config key";
			return;
		}

		my $locale_cfg = Config::Tiny->new->read($locale_config_file);
		my @langs = map { "\"$_\"" } keys $self->{lang_po_list};
		if ( @langs ) {
			$locale_cfg->{locale}{langs} = "[" . ( join ',', sort @langs ) . "]";
			$locale_cfg->write($locale_config_file);
		}

	}


#resource->generate_mo
	sub generate_mo {
		my $self = shift;
		my $generate_mo_script = "generate_mo.py";
		my $locale_config_file = $self->{data}{locale_config};
		if ( defined $locale_config_file ) {
			$locale_config_file =~ s/<project_root>/$self->{project_root}/;	
			if ( ! -f $locale_config_file ) {
				say "Not found 'tools/locale_config.ini' file";
				return;
			}
		} else { 
			say "generate_mo : resource not defined locale_config key";
			return; 
		}

		my $tools_dir = dirname $locale_config_file;
		if ( -x -f "$tools_dir/$generate_mo_script" ) {
			chdir $tools_dir;
			system "./$generate_mo_script";
		}
	}

#resource->source_file_list
	sub source_file_list {
		my $self = shift;
		my @keys = keys $self->{data};

		my $project_root = $self->{project_root};
		my @file_list = qx(find $project_root );
		@file_list = map { chomp $_; $_ } @file_list;

		my %regex_list;
		@keys = grep { /^source_file_regex(|\[.*\])$/ } @keys;

		for ( @keys ){
			my $option;
			if ( /^source_file_regex\[[^:]*:(.*)\]$/ ){
				$option = $1;
			} else {
				$option = "-k_";
			}
			my $regex = $self->{data}{$_};
			$regex =~ s/<project_root>/$project_root/;
			push @{ $regex_list{$option} }, $regex;
		}
	
		my %source_file_list;
		for my $option (sort keys %regex_list ){
			for my $f ( @file_list ){
				my $ref_regexs = $regex_list{$option};
				if ( any { $f =~ /$_/ } @$ref_regexs ){
					push @{ $source_file_list{$option} } , $f;
				}
			}
			
		}
		return \%source_file_list;
	}

	sub po_statistics {
		my $self = shift;
		my $ref_po_files = $self->{lang_po_list};
		my %po_stat;
		for ( sort keys %{ $ref_po_files } ){
			my ($lang,$file) = ($_ , $$ref_po_files{$_} );
			my $msgstat = qx(LC_ALL=C msgfmt --statistics -o /dev/null $file 2>&1);
			if ( $msgstat =~ /(\d+)\D+(\d+)?/ ) {
				my ( $trans, $untrans ) = ( $1, $2 );
				$untrans = defined $untrans ? $untrans : 0;
				$po_stat{$lang} = [ $trans, $untrans ];
			}
		}
		return \%po_stat;
	}

}

sub print_stat {
	my $ref_po_stat = shift;
	my $n =0;
	for my $lang ( sort keys %{ $ref_po_stat } ){
		my $ref_arr = $$ref_po_stat{$lang};
		my $trans = $ref_arr->[0]; 
		my $untrans =  $ref_arr->[1];
		my $total = $trans + $untrans;
		my $per  = $trans / $total; 
		$per = sprintf '%.1f', 100 * $per;
		$n++;
		print "\e[38;5;136;1m$lang\t\e[38;5;118;2m$trans/$untrans\t\e[38;5;123;2m$per%\t\e[0m";
		print "\n" if $n % 2 == 0; 
	}
	print "\n" if $n % 2;
}

sub install {
    my $cmd = "sudo apt-get install -y git transifex-client ";
	say '$ '. $cmd;
	system $cmd;
}
